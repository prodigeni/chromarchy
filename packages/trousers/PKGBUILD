# Maintainer: Thomas Sowell <tom@fancydriving.org>

buildarch=1

pkgname=trousers
pkgdesc="Open-source TCG Software Stack (TSS)"
pkgver=R30.4537
pkgrel=1
arch=('any')
url="http://git.chromium.org/gitweb/?p=chromiumos/third_party/trousers.git;a=summary"
license=('CPL')
_gitname='trousers'
makedepends=('git')

source=(trousers-user.patch
        trousers-no-error.patch
        tcsd.service
        tcsd.sh
        "$_gitname::git+http://git.chromium.org/git/chromiumos/third_party/trousers.git#branch=release-R30-4537.B")

md5sums=('c2c3a0f26d9c77f61ba7e3344305e8fc'
         'a08a286960d36569c46888a51ed92b95'
         '08c2f5a8c3a8ecfeb176c7b74267bb98'
         'ef8737208e939128210349c27608d6a3'
         'SKIP')

prepare() {
  cd "$srcdir/$_gitname"
  git apply ${srcdir}/trousers-user.patch
  git apply ${srcdir}/trousers-no-error.patch
}

build() {
  cd "$srcdir/$_gitname"

  sh bootstrap.sh
  ./configure --prefix=/usr
  make
}

package() {
  make -C "$srcdir/$_gitname" DESTDIR="${pkgdir}" install
  chmod 0600 ${pkgdir}/etc/tcsd.conf

  cd "$srcdir/$_gitname"
  install -d ${pkgdir}/etc/trousers
  install -m 644 dist/system.data.auth ${pkgdir}/etc/trousers
  install -m 644 dist/system.data.noauth ${pkgdir}/etc/trousers

  install -d ${pkgdir}/usr/lib/systemd/scripts
  install -m 755 ${srcdir}/tcsd.sh ${pkgdir}/usr/lib/systemd/scripts
  install -d ${pkgdir}/usr/lib/systemd/system
  install -m 644 ${srcdir}/tcsd.service ${pkgdir}/usr/lib/systemd/system
}
