# Maintainer: Thomas Sowell <tom@fancydriving.org>

buildarch=1

pkgname=libchromeos
pkgdesc="libchromeos"
pkgver=R30.4537
pkgrel=1
arch=('any')
url="http://git.chromium.org/gitweb/?p=chromiumos/platform/libchromeos;a=summary"
license=('custom:chromiumos')
_gitname='libchromeos'
_basever=180609
depends=('dbus-c++' 'gmock' 'chromiumos-libbase' 'dbus-glib')
makedepends=('git' 'protobuf' 'chromium-proto')

source=(libchromeos-pkgconfig.patch
        libchromeos-camera-proto.patch
        LICENSE
        "$_gitname::git+http://git.chromium.org/git/chromiumos/platform/libchromeos.git#branch=release-R30-4537.B")

md5sums=('dd67a33805136823a2110129c28be9a4'
         '17bfac5204793676c6e8572186b129be'
         '87dd8458232da630f5617873d42d8350'
         'SKIP')

prepare() {
  cd "$srcdir/$_gitname"
  git apply ${srcdir}/libchromeos-pkgconfig.patch
  git apply ${srcdir}/libchromeos-camera-proto.patch
}

build() {
  cd "$srcdir/$_gitname"

  BASE_VER=$_basever scons
}

package() {
  cd "$srcdir/$_gitname"

  install -d ${pkgdir}/usr/lib
  for I in libchromeos-*.so libpolicy-*.so; do
    install -m 644 -D ${I} ${pkgdir}/usr/lib/${I}
  done

  install -d ${pkgdir}/usr/lib/pkgconfig
  install -m 644 libchromeos-*.pc ${pkgdir}/usr/lib/pkgconfig

  install -d ${pkgdir}/usr/include/chromeos
  install -m 644 chromeos/*.h ${pkgdir}/usr/include/chromeos

  for I in dbus glib policy; do
    install -d ${pkgdir}/usr/include/chromeos/${I}
    install -m 644 chromeos/${I}/*.h ${pkgdir}/usr/include/chromeos/${I}
  done

  install -m 644 -D ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
