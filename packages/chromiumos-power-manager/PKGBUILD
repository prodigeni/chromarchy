# Maintainer: Thomas Sowell <tom@fancydriving.org>

buildarch=1

pkgname=chromiumos-power-manager
pkgdesc="Chromium OS power management daemon"
pkgver=R30.4537
pkgrel=1
arch=('any')
url="http://git.chromium.org/gitweb/?p=chromiumos/platform/power_manager.git;a=summary"
license=('custom:chromiumos')
_gitname='power_manager'
depends=('dbus-glib' 'libchromeos' 'chromiumos-metrics' 'chromiumos-system-api'
         'shflags' 'syslog-ng' 'vboot-utils')
makedepends=('git')

source=(power-manager-stack.patch
        power-manager-dbus-headers.patch
        power-manager-systemd.patch
        power-manager-utmp.patch
        power-manager-scripts.patch
        power-manager-oobe.patch
        powerd.service
        useractd.c
        useractd.service
        LICENSE
        "$_gitname::git+http://git.chromium.org/git/chromiumos/platform/power_manager.git#branch=release-R30-4537.B")

md5sums=('b2f15e9f1002389c1f6db7cef8bc08d2'
         '9f1fb87508c0e1c2de971f92bf958537'
         '2c00d83f83d8a11efaa01c4f4e96e7e1'
         '5d61037e6c04fd68a8aa75a07866e8b2'
         'e4e3a1463e5cb2df91731a029e41fe7b'
         '65a2c5d81e53883dddeea0d57fd247d3'
         '440654d8bfb2ef17437f03ee75672c25'
         '0796112cd6519fb94e84acc34be51250'
         '5e4935c4b51374ab5522eb0c337b07b7'
         '87dd8458232da630f5617873d42d8350'
         'SKIP')

prepare() {
  cd "$srcdir/$_gitname"
  git apply ${srcdir}/power-manager-stack.patch
  git apply ${srcdir}/power-manager-dbus-headers.patch
  git apply ${srcdir}/power-manager-systemd.patch
  git apply ${srcdir}/power-manager-utmp.patch
  git apply ${srcdir}/power-manager-scripts.patch
  git apply ${srcdir}/power-manager-oobe.patch
}

build() {
  cd "$srcdir/$_gitname"

  local _pm_protos=(
    input_event.pb.cc
    peripheral_battery_status.pb.cc
    policy.pb.cc
    power_supply_properties.pb.cc
    suspend.pb.cc
  )
  for I in "${_pm_protos[@]}"; do
    make power_manager/$I
  done

  USE_NEW_POWER_BUTTON=y USE_ALS=y USE_LOCKVT=y CXXFLAGS="-Wno-error=deprecated-declarations -I${srcdir}/$_gitname" make

  local _useractd_CFLAGS=$(pkg-config --cflags glib-2.0 dbus-glib-1)
  local _useractd_LDFLAGS=$(pkg-config --libs glib-2.0 dbus-glib-1)

  cd "$srcdir"
  gcc $_useractd_CFLAGS $_useractd_LDFLAGS useractd.c -o useractd
}

package() {
  cd "$srcdir/$_gitname"

  install -d ${pkgdir}/usr/bin
  local _bins=(
    out/powerd/powerd
    out/powerd/powerd_setuid_helper
    out/tools/backlight_dbus_tool
    out/tools/backlight_tool
    out/tools/memory_suspend_test
    out/tools/powerd_dbus_suspend
    out/tools/power_supply_info
    out/tools/set_power_policy
    out/tools/suspend_delay_sample
  )
  local _scripts=(
    powerd/powerd_suspend
    tools/activate_short_dark_resume
    tools/debug_sleep_quickly
    tools/send_metrics_on_resume
    tools/set_short_powerd_timeouts
    tools/suspend_stress_test
  )
  for I in "${_bins[@]}" "${_scripts[@]}"; do
    install -m 755 $I ${pkgdir}/usr/bin
  done

  install -d ${pkgdir}/usr/share/power_manager
  install -m 644 config/* ${pkgdir}/usr/share/power_manager

  install -d ${pkgdir}/etc/dbus-1/system.d
  install -m 644 dbus/org.chromium.PowerManager.conf \
    ${pkgdir}/etc/dbus-1/system.d

  install -d ${pkgdir}/lib/udev
  install -m 755 udev/usb-hid-wake.sh ${pkgdir}/lib/udev

  install -d ${pkgdir}/lib/udev/rules.d
  install -m 644 udev/99-usb-hid-wake.rules ${pkgdir}/lib/udev/rules.d

  install -d ${pkgdir}/usr/lib/systemd/system
  install -m 644 ${srcdir}/powerd.service ${pkgdir}/usr/lib/systemd/system

  install -d ${pkgdir}/var/log/power_manager
  install -d ${pkgdir}/var/lib/power_manager

  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m 644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE


  cd "$srcdir"

  install -d ${pkgdir}/usr/bin
  install -m 755 useractd ${pkgdir}/usr/bin

  install -m 644 -D useractd.service ${pkgdir}/usr/lib/systemd/system

  install -m 644 -D ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

